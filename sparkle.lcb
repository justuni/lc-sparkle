library community.lvecode.trevordevore.sparkle

  use com.livecode.foreign
  use com.livecode.objc
  use com.livecode.engine

  metadata title is "Sparkle"
  metadata author is "TrevorDeVore"
  metadata version is "0.0.1"

  private foreign handler ObjC_SUUpdaterSharedUpdater() \
        returns ObjcId \
        binds to "objc:Sparkle>SUUpdater.+sharedUpdater"

  private foreign handler ObjC_SUUpdaterSetDelegate (in pSUUpdater as ObjcId, in pDelegate as ObjcId) \
        returns nothing \
        binds to "objc:Sparkle>SUUpdater.-setDelegate:"

  private foreign handler ObjC_SUUpdaterAutomaticChecking (in pSUUpdater as ObjcId) \
        returns CBool \
        binds to "objc:Sparkle>SUUpdater.-automaticallyChecksForUpdates"
  private foreign handler ObjC_SUUpdaterUpdaterMayCheckForUpdates (in pSUUpdater as ObjcId) \
        returns CBool \
        binds to "objc:Sparkle>SUUpdater.-updaterMayCheckForUpdates"
  private foreign handler ObjC_SUUpdaterAutomaticallyChecksForUpdates (in pSUUpdater as ObjcId) \
        returns CBool \
        binds to "objc:Sparkle>SUUpdater.-automaticallyChecksForUpdates"
  private foreign handler ObjC_SUUpdaterAutomaticallyDownloadsUpdates (in pSUUpdater as ObjcId) \
        returns CBool \
        binds to "objc:Sparkle>SUUpdater.-automaticallyDownloadsUpdates"
  -- (NSURL *)feedURL
  private foreign handler ObjC_SUUpdaterFeedURL (in pSUUpdater as ObjcId) \
        returns ObjcId \
        binds to "objc:Sparkle>SUUpdater.-feedURL"
  -- (NSString *)userAgentString
  private foreign handler ObjC_SUUpdaterUserAgentString (in pSUUpdater as ObjcId) \
        returns ObjcId \
        binds to "objc:Sparkle>SUUpdater.-userAgentString"
  private foreign handler ObjC_SUUpdaterSendsSystemProfile (in pSUUpdater as ObjcId) \
        returns CBool \
        binds to "objc:Sparkle>SUUpdater.-sendsSystemProfile"
  -- (NSURL *)parameterizedFeedURL
  private foreign handler ObjC_SUUpdaterParameterizedFeedURL (in pSUUpdater as ObjcId) \
        returns ObjcId \
        binds to "objc:Sparkle>SUUpdater.-parameterizedFeedURL"
  -- (NSTimeInterval)updateCheckInterval
  private foreign handler ObjC_SUUpdaterUpdateCheckInterval (in pSUUpdater as ObjcId) \
        returns ObjcId \
        binds to "objc:Sparkle>SUUpdater.-updateCheckInterval"
  private foreign handler ObjC_SUUpdaterUpdateInProgress (in pSUUpdater as ObjcId) \
        returns CBool \
        binds to "objc:Sparkle>SUUpdater.-updateInProgress"

  variable mSUUpdaterDelegate as optional ObjcObject


  public handler sparkleInitialize() returns nothing
    variable sharedUpdater as ObjcObject

    unsafe
      put ObjC_SUUpdaterSharedUpdater() into sharedUpdater
      SetSUUpdaterInformalDelegate(sharedUpdater)
    end unsafe
  end handler


  foreign handler Delegate_SUUpdaterDidFinishLoadingAppcast (in pUpdater as ObjcId, in pAppcast as ObjcId) \
        returns nothing binds to "objc:.-updater:didFinishLoadingAppcast:"
  foreign handler Delegate_SUUpdaterDidFindValidUpdate (in pUpdater as ObjcId, in pAppcastItem as ObjcId) \
        returns nothing binds to "objc:.-updater:didFindValidUpdate:"
  foreign handler Delegate_SUUpdaterDidNotFindUpdate (in pUpdater as ObjcId) \
        returns nothing binds to "objc:.-updaterDidNotFindUpdate:"
  foreign handler Delegate_SUUpdaterWillInstallUpdate (in pUpdater as ObjcId, in pAppcastItem as ObjcId) \
        returns nothing binds to "objc:.-updater:willInstallUpdate:"
  foreign handler Delegate_SUUpdaterWillRelaunchApplication (in pUpdater as ObjcId) \
        returns nothing binds to "objc:.-updaterWillRelaunchApplication:"
  foreign handler Delegate_SUUpdaterWillShowModalAlert (in pUpdater as ObjcId) \
        returns nothing binds to "objc:.-updaterWillShowModalAlert:"
  foreign handler Delegate_SUUpdaterDidShowModalAlert (in pUpdater as ObjcId) \
        returns nothing binds to "objc:.-updaterDidShowModalAlert:"


  unsafe handler SetSUUpdaterInformalDelegate(in pSUUpdater as ObjcObject) returns nothing
    variable tDelegate as optional ObjcObject
    variable tForeignHandlers as List
    variable tMapping as Array

    put [Delegate_SUUpdaterDidFinishLoadingAppcast, \
          Delegate_SUUpdaterDidFindValidUpdate, \
          Delegate_SUUpdaterDidNotFindUpdate, \
          Delegate_SUUpdaterWillInstallUpdate, \
          Delegate_SUUpdaterWillRelaunchApplication, \
          Delegate_SUUpdaterWillShowModalAlert, \
          Delegate_SUUpdaterDidShowModalAlert] into tForeignHandlers

    put {"updater:didFinishLoadingAppcast:": DidFinishLoadingAppcast, \
          "updater:didFindValidUpdate:": DidFindValidUpdate, \
          "updaterDidNotFindUpdate:": DidNotFindUpdate, \
          "updater:willInstallUpdate:": WillInstallUpdate, \
          "updaterWillRelaunchApplication:": WillRelaunchApplication, \
          "updaterWillShowModalAlert:": WillShowModalAlert, \
          "updaterDidShowModalAlert:": DidShowModalAlert} into tMapping

    put CreateObjcInformalDelegate( \
              tForeignHandlers, \
              tMapping) into tDelegate

    if tDelegate is not nothing then
      put tDelegate into mSUUpdaterDelegate
      ObjC_SUUpdaterSetDelegate(pSUUpdater, mSUUpdaterDelegate)
    end if
  end handler


  public handler sparkleGetAutomaticChecking() returns Boolean
    variable sharedUpdater as ObjcObject

    unsafe
      put ObjC_SUUpdaterSharedUpdater() into sharedUpdater
      return ObjC_SUUpdaterAutomaticChecking(sharedUpdater)
    end unsafe
  end handler


  public handler sparkleGetUpdaterMayCheckForUpdates() returns Boolean
    variable sharedUpdater as ObjcObject

    unsafe
      put ObjC_SUUpdaterSharedUpdater() into sharedUpdater
      return ObjC_SUUpdaterUpdaterMayCheckForUpdates(sharedUpdater)
    end unsafe
  end handler


  public handler sparkleSetUpdaterMayCheckForUpdates(in pBoolean as Boolean) returns nothing
    variable sharedUpdater as ObjcObject

    unsafe
      put ObjC_SUUpdaterSharedUpdater() into sharedUpdater
      -- return ObjC_SUUpdaterSetUpdaterMayCheckForUpdates(sharedUpdater, pBoolean)
    end unsafe
  end handler


  handler DidFinishLoadingAppcast(in pUpdater as ObjcId, in pAppcast as ObjcId) returns nothing
    post "sparkleFinishedLoadingAppcast"
  end handler

  handler DidFindValidUpdate(in pUpdater as ObjcId, in pAppcastItem as ObjcId) returns nothing
    post "sparkleFoundUpdate"
  end handler

  handler DidNotFindUpdate(in pUpdater as ObjcId) returns nothing
    post "sparkleDidNotFindUpdate"
  end handler

  handler WillInstallUpdate(in pUpdater as ObjcId, in pAppcastItem as ObjcId) returns nothing
    post "sparkleWillInstallUpdate"
  end handler

  handler WillRelaunchApplication(in pUpdater as ObjcId) returns nothing
    post "sparkleWillRelaunchApplication"
  end handler

  handler WillShowModalAlert(in pUpdater as ObjcId) returns nothing
    post "sparkleWillShowModalAlert"
  end handler

  handler DidShowModalAlert(in pUpdater as ObjcId) returns nothing
    post "sparkleDidShowModalAlert"
  end handler

end library
